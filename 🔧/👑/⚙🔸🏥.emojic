💭🔜
    clihelper - A CLI Library for Emojicode.

    Copyright 2022 Nathan Mentley
    
    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
    
    The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
    
    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
🔚💭

📗 🏥 The basic help command. This command will display a help message based on the configured 🚂. 📗
🌍 🐇 ⚙🔸🏥 🍇
  🐊 ⚙

  🖍🆕 app 🚂
  
  🆕 🍼 app 🚂 🍇🍉

  📗 Returns a collection of command parameter definitions 📗
  🔓 ❓ 🖇 ➡️  🍨🐚📎🍆🍇
    ↩️ 🆕🍨🐚📎🍆❗️
  🍉

  📗 Returns a collection of command parameter definitions 📗
  🔓 ❓ 📛 ➡️ 🔡🍇
    ↩️ 🔤Help🔤
  🍉

  📗 Returns a description of the command. This is used for the help display to explain the command to the user. 📗
  🔓 ❓ 💬 ➡️ 🔡🍇
    ↩️ 🔤Displays a help message🔤
  🍉

  📗 Runs the command logic. This logic should return an int that will be used as the exit code. 📗
  🔓 ❗️ 🏃 args 🍯🐚🔡🍆 ➡️ 🔢🍇
    🔗 app ❓ ➡️  commands

    🖌🐇🔶🦎🖌
      📛app❓
      🍿
        🆕🔶🦎🎨▶💙❗️
        🆕🔶🦎🎨▶🌩❗️
        🆕🔶🦎🎨▶🌧❗️
      🍆
    ❗️ ➡️ appName
    🖌🐇🔶🦎🖌
      💬app❓
      🍿
        🆕🔶🦎🎨▶🌧❗️
      🍆
    ❗️ ➡️ appDesc

    🆕🔠❗️➡️ sBuilder
    🐻sBuilder appName❗️
    🐻sBuilder 🔤 - 🔤❗️
    🐻sBuilder appDesc❗️
    😀🔡sBuilder❗️❗️

    🔂 key 🐙commands❗️ 🍇
      🐽 commands key ❗️ ➡️  command
      ↪️ command➡️ command🍇
        ↪️ ⚙command❓➡️ command🍇
          🖌🐇🔶🦎🖌
            📛command❓
            🍿
              🆕🔶🦎🎨▶💛❗️
            🍆
          ❗➡️ commandName

          🆕🔠❗️➡️ sBuilder2
          🐻sBuilder2 🔤    🔤❗️
          🐻sBuilder2 commandName❗️
          🐻sBuilder2 🔤 - 🔤❗️
          🐻sBuilder2
            🖌🐇🔶🦎🖌
              💬command❓
              🍿
                🆕🔶🦎🎨▶🌧❗️
              🍆
            ❗
          ❗️
          😀🔡sBuilder2❗️❗️

          🔂 param 🖇 command ❓ 🍇
            💭 format parameter name
            🖌🐇🔶🦎🖌
              📛param❓
              🍿
                🆕🔶🦎🎨▶💚❗️
              🍆
            ❗➡️ paramName
  
            💭 use string builder to build parameter string
            🆕🔠❗️➡️ sBuilder3
            🐻sBuilder3 🔤❌t🔤❗️
            🐻sBuilder3 paramName❗️

            💭 add key
            🐻sBuilder3 🔤❌t --🔤❗️
            🐻sBuilder3 🔑param❓❗️

            💭 add optional short if it exists
            ↪️ 🧒param❓ ➡️ short 🍇
              🐻sBuilder3 🔤❌t -🔤❗️
              🐻sBuilder3 short❗️
            🍉

            💭 add optional default if it exists
            ↪️ 🚗param❓ ➡️ default 🍇
              🐻sBuilder3 🔤❌t default value: 🔤❗️
              🐻sBuilder3 
                🖌🐇🔶🦎🖌
                  default
                  🍿
                    🆕🔶🦎🎨▶💙❗️
                  🍆
                ❗
              ❗️
            🍉

            🐻sBuilder3 🔤❌n❌t❌t🔤❗️
            🐻sBuilder3 
              🖌🐇🔶🦎🖌
                💬param❓
                🍿
                  🆕🔶🦎🎨▶🌧❗️
                🍆
              ❗
            ❗️
            😀🔡sBuilder3❗️❗️
          🍉
        🍉
      🍉
    🍉

    ↩️ 0
  🍉
🍉
