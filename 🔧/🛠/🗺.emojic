💭🔜
    clihelper - A CLI Library for Emojicode.

    Copyright 2019 Nathan Mentley
    
    Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
    
    The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
    
    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
🔚💭

📗 🗺 contains and describes the context in which the app command was executed. Extra arguments, working directory, whatever the state was that was passed to this logic. 📗
🐇 🗺 🍇
  🖍🆕 args 🍨🐚🔡🍆
  🖍🆕 params 🍬🍯🐚🔡🍆
  
  💭 init
  🆕 🍇
    🎞🐇💻❗️ ➡️  🖍 args
    🤷 ➡️  🖍 params
  🍉

  📗 Returns the passed command name 📗
  🔓 ❗️ 📛 ➡️ 🍬🔡🍇
    ↪️ 🐔args❗️ ▶️ 1 🍇
      ↩️ 🐽args 1❗️
    🍉
    
    ↩️ 🤷
  🍉

  📗 Pulls all the parameters out into a dictionary 📗
  🔓 ❗️ 🏞 parameterDefs 🍨🐚📎🍆 ➡️  🍯🐚🔡🍆 🍇
    ↪️  params 🙌 🤷 🍇
      🆕 🍯🐚🔡🍆 🐸❗️ ➡️  🖍🆕 ret
      
      🔂 def parameterDefs 🍇
        🌲 🐕 def ❗️ ➡️  value
        
        ↪️  value ➡️  value 🍇
          value ➡️  🐽 ret 📪 🔑 def ❓ ❗️ ❗️
        🍉
        🙅 🍇
          💭 if no variable was passed store blank. Should we just not store it at all?
          🔤no value🔤 ➡️  🐽 ret 📪 🔑 def ❓ ❗️ ❗️
        🍉
      🍉

      ret ➡️ 🖍 params
    🍉
    
    ↩️  🍺 params
  🍉

  📗 Pulls a single parameter from the cli arguments from a 📎 [parameter def] 📗
  🔓 ❗️ 🌲 def 📎 ➡️  🍬🔡 🍇
    👎 ➡️  🖍🆕 valueIsNext

    🔂 arg args 🍇
      ↪️  valueIsNext 🍇
        ↩️ arg
      🍉

      ↪️  🍪🔤--🔤 🔑def❓🍪 🙌 arg 🍇
        👍 ➡️  🖍 valueIsNext
      🍉
      ↪️  🧒def❓ ➡️  short 🍇
        ↪️  🍪🔤-🔤 short🍪 🙌 arg 🍇
          👍 ➡️  🖍 valueIsNext
        🍉
      🍉
    🍉

    ↪️  🚗def❓ ➡️  default 🍇
      ↩️ default
    🍉
    🙅 🍇
      ↩️ 🔤🔤
    🍉
  🍉
🍉
